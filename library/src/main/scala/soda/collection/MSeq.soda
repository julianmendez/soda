package soda.collection

class MSeq [A]

  abstract
    isEmpty : Boolean

  opt [B] (ifEmpty : B) (ifNonEmpty : NESeq [A] -> B) : B =
    match this
      case ESeq_ () ==> ifEmpty
      case NESeq_ (head, tail) ==> ifNonEmpty (NESeq_ (head, tail) )
    end

end

class MSeqRec [A]

  @tailrec
  _tailrec_fold_while [B] (sequence : MSeq [A] ) (current_value : B) (next_value_function : B -> A -> B) (condition : B -> A -> Boolean) : B =
    match sequence
      case ESeq_ () ==> current_value
      case NESeq_ (head, tail) ==>
        if not condition (current_value) (head)
        then current_value
        else _tailrec_fold_while (tail) (next_value_function (current_value) (head) ) (next_value_function) (condition)
      end

  fold_while [B] (sequence : MSeq [A] ) (initial_value : B) (next_value : B -> A -> B) (condition : B -> A -> Boolean) : B =
    _tailrec_fold_while (sequence) (initial_value) (next_value) (condition)

end

class ESeq [A]
  extends
    MSeq [A]

  isEmpty = true

end

class NEMSeq [A]
  extends
    MSeq [A]

  abstract
    head0 : A
    tail0 : MSeq [A]

  isEmpty = false

end

class NESeq [A]
  extends
    NEMSeq [A]

  abstract
    head0 : A
    tail0 : MSeq [A]

  head : A = head0

  tail : MSeq [A] = tail0

end

