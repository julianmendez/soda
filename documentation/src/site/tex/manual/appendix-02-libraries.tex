\chapter{Libraries}

\begin{lstlisting}[label={lst:fold}]
  @tailrec
  _tailrec_fold4 [A, B, C subtype B] (sequence: Seq [A], current_value: C, next_value_function: (B, A) -> C, condition: (B, A) -> Boolean): C =
    if sequence.isEmpty
    then current_value
    else
      if not condition (current_value, sequence.head)
      then current_value
      else _tailrec_fold4 (sequence.tail, next_value_function (current_value, sequence.head), next_value_function, condition)

  fold [A, B, C subtype B] (sequence: Seq [A], initial_value: C, next_value_function: (B, A) -> C, condition: (B, A) -> Boolean): C =
    _tailrec_fold4 (sequence, initial_value, next_value_function, condition)
\end{lstlisting}

\begin{lstlisting}[label={lst:range}]
  @tailrec
  _tailrec_range (n: Int, sequence: Seq [Int]): Seq [Int] =
    if n <= 0
    then sequence
    else _tailrec_range (n - 1, sequence.+: (n - 1))

  range (length: Int): Seq [Int] =
    _tailrec_range (length, Seq [Int] ())
\end{lstlisting}


\begin{lstlisting}[label={lst:option}]
class OptionSD [A] =

  abstract
    opt [B] (ifEmpty: B, ifNonEmpty: A -> B): B
    isEmpty: Boolean
    getOrElse (default: A): A
    fold [B] (ifEmpty: B, f: A -> B): B
    flatMap [B] (mapping: A -> OptionSD [B]): OptionSD [B]

end

class NoneSD [A]
  extends OptionSD [A] =

  opt [B] (ifEmpty: B, ifNonEmpty: A -> B): B = ifEmpty

  isEmpty: Boolean = true

  getOrElse (default: A): A = default

  fold [B] (ifEmpty: B, f: A -> B): B = opt (ifEmpty, f)

  flatMap [B] (mapping: A -> OptionSD [B]): OptionSD [B] = NoneSD_ [B] ()

end

class NoneSD_ [A] ()
  extends NoneSD [A]

class SomeSD [A]
  extends OptionSD [A] =

  abstract
    element: A

  value: A = element

  opt [B] (ifEmpty: B, ifNonEmpty: A -> B): B = ifNonEmpty (element)

  isEmpty: Boolean = false

  getOrElse (default: A): A = element

  fold [B] (ifEmpty: B, f: A -> B): B = opt (ifEmpty, f)

  flatMap [B] (mapping: A -> OptionSD [B]): OptionSD [B] = mapping (element)

end

class SomeSD_ [A] (element: A)
  extends SomeSD [A]
\end{lstlisting}

