package soda.example.forcoq.lib

class list [A]

end

class nil [A]
  extends
    list [A]

end

class cons [A]
  extends
    list [A]

  abstract
    e : A
    s : list [A]

end

class SeqList

  @tailrec
  _tailrec_reverse [A] (a : list [A] ) (b : list [A] ) : list [A] =
    match a
      case cons_ (e, s) ==> _tailrec_reverse (s) ( cons_ (e, b) )
      case x ==> b
    end

  reverse [A] (s : list [A] ) : list [A] =
    _tailrec_reverse [A] (s) (nil_ [A] () )

  @tailrec
  _tailrec_from_Seq [A] (a : Seq [A] ) (b : list [A] ) : list [A] =
    match a
      case (e) :: (s) ==> _tailrec_from_Seq (s) (cons_ (e, b) )
      case x ==> b
    end

  from_Seq [A] (a : Seq [A] ) : list [A] =
    reverse (_tailrec_from_Seq (a) ( nil_ [A] () ) )

  @tailrec
  _tailrec_to_Seq [A] (a : list [A] ) (b : Seq [A] ) : Seq [A] =
    match a
      case cons_ (e, s) ==> _tailrec_to_Seq (s) ( b .+: (e) )
      case x ==> b
    end

  to_Seq [A] (a : list [A] ) : Seq [A] =
    (_tailrec_to_Seq (a) ( Seq [A]() ) ) .reverse

end

