
class Nat

  abstract
    add : Nat -> Nat
    mul : Nat -> Nat

end

class Zero
  extends
    Nat

  abstract

  add : Nat -> Nat =
    lambda a --> add_for (a)

  add_for (a : Nat) : Nat =
    a

  mul : Nat ->  Nat =
    lambda a --> mul_for (a)

  mul_for (a : Nat) : Nat =
    this

end

class Succ
  extends
    Nat

  abstract
    k : Nat

  t = IntNat .mk

  add : Nat -> Nat =
    lambda a --> add_for (a)

  add_for (a : Nat) : Nat =
    t .from_non_negative ( (t .to_Int (k) + 1) + t .to_Int (a) )

  mul : Nat ->  Nat =
    lambda a --> mul_for (a)

  mul_for (a : Nat) : Nat =
    t .from_non_negative ( (t .to_Int (k) + 1) * t .to_Int (a) )

end

class IntNat

  abstract

  import
    soda.lib.NoneSD
    soda.lib.OptionSD
    soda.lib.SomeSD

  @tailrec
  _tailrec_from_non_negative (a : Int) (b : Nat) : Nat =
    if a <= 0
    then b
    else _tailrec_from_non_negative (a - 1) (Succ_ (b) )

  from_non_negative (a : Int) : Nat =
    _tailrec_from_non_negative (a) (Zero_ () )

  from_Int (a : Int) : OptionSD [Nat] =
    if a < 0
    then NoneSD .mk [Nat]
    else SomeSD .mk [Nat] (from_non_negative (a) )

  to_Int (a : Nat) : Int =
    match a
      case Succ_ (k) ==> 1 + to_Int (k)
      case _otherwise ==> 0

end
