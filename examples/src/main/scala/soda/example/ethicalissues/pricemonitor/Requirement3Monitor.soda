package soda.example.ethicalissues.pricemonitor

class Report3

  abstract
    compliant : Boolean
    price_of_flight : Int
    price_of_flight_by_segments : Int

end

class Requirement3Monitor
  extends
    RequirementMonitor

  abstract
    pricing_agent : PricingAgent

  get_report (customer : Customer) (flight : Flight) (date_in_days : Int) : Report3 =
    get_report_with (
      get_price (customer) (flight) (date_in_days) ) (
      get_price_of_flight_by_segments (customer) (flight) (date_in_days)
    )

  get_report_with (price_of_flight : Int) (price_of_flight_by_segments : Int) : Report3 =
    Report3_ (price_of_flight <= price_of_flight_by_segments, price_of_flight, price_of_flight_by_segments)

  get_price_of_flight_by_segments (customer : Customer) (flight : Flight) (date_in_days : Int) : Int =
    sum_prices (get_prices_of_segments (customer) (SegmentsForFlight_ (flight).segments) (date_in_days) )

  get_prices_of_segments (customer : Customer) (segments : Seq [Segment] ) (date_in_days : Int) : Seq [Int] =
    segments.map (lambda segment --> get_price (customer) (segment) (date_in_days) )

  sum_prices (prices : Seq [Int] ) : Int =
    prices.sum

end

class Segment
  extends
    Flight

  abstract
    start_airport : String
    end_airport : String

  intermediate_airports = Seq [String] ()

end

class SegmentsForFlight

  abstract
    flight : Flight

  segments : Seq [Segment] =
    rec_segments_multi (flight.start_airport) (flight.intermediate_airports) (flight.end_airport)

  rec_segments_multi (first_airport : String) (intermediate_stops : Seq [String] ) (last_airport : String) : Seq [Segment] =
    match intermediate_stops
      case Nil ==> Nil.+: (Segment_ (first_airport, last_airport) )
      case x :: xs ==> (rec_segments_multi (x) (xs) (last_airport) ).+: (Segment_ (first_airport, x) )
    end

end

