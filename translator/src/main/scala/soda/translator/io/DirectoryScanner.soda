
/**
 * This class is used to scan files in a given directory.
 */

class DirectoryScanner

  abstract

  import
    java.io.File

  _get_files_to_scan_with (to_scan_head : File) (to_scan_tail : Seq [File] ) : Seq [File] =
    if to_scan_head .isDirectory
    then to_scan_tail .++ (_list_files (to_scan_head) )
    else to_scan_tail

  _get_files_to_scan (to_scan : Seq [File] ) : Seq [File] =
    if to_scan .isEmpty
    then to_scan
    else _get_files_to_scan_with (to_scan .head) (to_scan .tail)

  @tailrec
  _tailrec_scan (found : Seq [File] ) (to_scan : Seq [File] ) : Seq [File] =
    if to_scan .isEmpty
    then found
    else _tailrec_scan (found .+: (to_scan .head) ) (_get_files_to_scan (to_scan) )

  _scan (found : Seq [File] ) (to_scan : Seq [File] ) : Seq [File] =
    _tailrec_scan (found) (to_scan)

  _list_files_with (files : Option [Array [File] ] ) : Seq [File] =
    match files
      case Some (array) ==> array .toSeq
      case _otherwise ==> Seq ()

  _list_files (to_scan_head : File) : Seq [File] =
    _list_files_with (Option (to_scan_head .listFiles) )

  get_all_files (start : File) : Seq [File] =
    if start .isFile
    then Seq (start)
    else _scan (Seq () ) (start .listFiles () .toSeq)

end

