package soda.translator.io

class AbstractDirectoryProcessor

  abstract
    start : String
    process_soda_file : java.io.File -> Boolean

end

class DirectoryProcessor
  extends
    AbstractDirectoryProcessor

  abstract
    start : String
    process_soda_file : java.io.File -> Boolean

  import
    java.io.File

  soda_suffix = ".soda"

  _all_files =
    DirectoryScanner_ ().get_all_files ( @new File (start))

  _soda_files =
    _all_files
      .filter ( lambda x --> x.isFile)
      .filter ( lambda x --> x.getName.endsWith (soda_suffix))

  _lib_files =
    _all_files
      .filter ( lambda x --> x.isFile)
      .filter ( lambda file --> file.getName == LibraryDeployer_ ().library_marker_file)

  process () : Boolean =
    LibraryDeployer_ ().expand_library (_lib_files) and
      _soda_files
        .map (process_soda_file)
        .forall ( lambda x --> x)

end

