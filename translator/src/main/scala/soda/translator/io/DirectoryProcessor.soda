package soda.translator.io


* DirectoryProcessor = {
  + java.io.File

  has start: String

  has process_soda_file: File -> Boolean

  Soda_suffix = ".soda"

  all_files = DirectoryScannerImpl().get_all_files(@new File(start))

  soda_files =
    all_files
      .filter(x -> x.isFile)
      .filter(x -> x.getName.endsWith(Soda_suffix))

  lib_files =
    all_files
      .filter(x -> x.isFile)
      .filter(file -> file.getName == LibraryDeployerImpl().Library_marker_file)

  process(): Boolean =
    LibraryDeployerImpl().expand_library(lib_files) and
      soda_files
        .map(process_soda_file)
        .forall(x -> x)
}

* DirectoryProcessorImpl(start: String, process_soda_file: java.io.File -> Boolean)
  extends DirectoryProcessor

