
class AbstractDirectoryProcessor

  abstract
    start : String
    process_soda_file : java.io.File -> Boolean

end

class DirectoryProcessor
  extends
    AbstractDirectoryProcessor

  abstract
    start : String
    process_soda_file : java.io.File -> Boolean

  import
    java.io.File

  soda_suffix = ".soda"

  package_file_name = "Package.soda"

  _all_files : Seq [File] =
    DirectoryScanner .mk .get_all_files ( @new File (start) )

  _sorted_all_soda_files : Seq [File] =
    _all_files
      .filter (lambda x --> x .isFile)
      .filter (lambda x --> x .getName .endsWith (soda_suffix) )
      .distinct
      .sorted

  _sorted_package_files : Seq [File] =
    _all_files
      .filter (lambda x --> x .isFile)
      .filter (lambda x --> x .getName == package_file_name)
      .distinct
      .sorted

  _sorted_soda_directories : Seq [File] =
    _sorted_all_soda_files
      .map (lambda x --> x .getParentFile)
      .distinct
      .sorted

  _sorted_package_file_directories : Seq [File] =
    _sorted_package_files
      .map (lambda x --> x .getParentFile)
      .distinct
      .sorted

  _sorted_soda_non_package_files : Seq [File] =
    _sorted_all_soda_files
      .filter (lambda x --> not (x .getName == package_file_name) )

  _original_soda_files : Seq [File] =
    _sorted_package_files  .++ (_sorted_soda_non_package_files)

  _missing_package_files : Seq [File] =
    _sorted_soda_directories
      .filter (lambda x --> not (_sorted_package_file_directories .contains (x) ) )
      .map (lambda x --> @new File (x , package_file_name) )

  _soda_files_and_missing_package_files : Seq [File] =
    _missing_package_files .++ (_original_soda_files)

  _sorted_lib_files : Seq [File] =
    _all_files
      .filter (lambda x --> x .isFile)
      .filter (lambda file --> file .getName == LibraryDeployer .mk .library_marker_file)
      .sorted

  _expand_library_files (ready : Boolean) : Boolean =
    ready and
      LibraryDeployer .mk .expand_library (_sorted_lib_files)

  _add_missing_package_files (ready : Boolean) : Boolean =
    ready and
      _missing_package_files
       .map (lambda x --> x .createNewFile)
       .forall (lambda x --> x)

  _process_all_soda_files (ready : Boolean) : Boolean =
    ready and
      _soda_files_and_missing_package_files
        .map (process_soda_file)
        .forall (lambda x --> x)

  process () : Boolean =
    Some (true)
      .map (_expand_library_files)
      .map (_add_missing_package_files)
      .map (_process_all_soda_files)
      .getOrElse (false)

end

