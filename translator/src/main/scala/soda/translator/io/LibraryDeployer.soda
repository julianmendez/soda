package soda.translator.io

class LibraryDeployer

  import
    java.io.File

  library_marker_file = "lib.soda"

  library_directory_in_jar = "/lib/soda/lib/"

  library_content_files : Seq [String] =
    SimpleFileReader_ ()
      .read_resource (library_directory_in_jar + "files.txt")
      .split ("\n")
      .toSeq

  expand_library (lib_files : Seq [File] ) : Boolean =
    lib_files
      .map (lambda lib_file -> lib_file.getParent)
      .map (lambda parent_directory -> expand_files (parent_directory) )
      .forall (lambda x -> x)

  expand_files (parent_directory : String) : Boolean =
    library_content_files
      .map (lambda lib_file_name ->
        SimpleFileWriter_ ().write_file_with (
          file := SimpleFileWriter_ ().create_file (parent_directory) (lib_file_name) ) (
          content := SimpleFileReader_ ().read_resource (library_directory_in_jar + lib_file_name)
        ) )
      .forall (lambda x -> x)

end

