
class LibraryDeployer

  abstract

  import
    java.io.File

  library_marker_file = "lib.soda"

  _library_directory_in_jar = "/lib/soda/lib/"

  _library_content_files : Seq [String] =
    SimpleFileReader .mk
      .read_resource (_library_directory_in_jar + "files.txt")
      .split ("\n")
      .toSeq

  _expand_files (parent_directory : String) : Boolean =
    _library_content_files
      .map (lambda lib_file_name -->
        SimpleFileWriter .mk .write_file_with (
          file := SimpleFileWriter .mk .create_file (parent_directory) (lib_file_name) ) (
          content := SimpleFileReader .mk .read_resource (
            _library_directory_in_jar + lib_file_name)
        ) )
      .forall (lambda x --> x)

  expand_library (lib_files : Seq [File] ) : Boolean =
    lib_files
      .map (lambda lib_file --> lib_file .getParent)
      .map (lambda parent_directory --> _expand_files (parent_directory) )
      .forall (lambda x --> x)

end

