
class AbstractHelp
  extends
    soda.translator.extension.common.Extension

  abstract
    execute : Seq [String] -> Boolean

  import
    soda.lib.SomeSD
    soda.translator.io.SimpleFileReader

  path : String = "/soda/translator/documentation/"

  read (file_name : String) : String =
    SimpleFileReader .mk .read_resource (path + file_name)

  this_package = this .getClass .getPackage

  name : String = Option (this_package .getImplementationTitle) .getOrElse ("")

  version : String = Option (this_package .getImplementationVersion) .getOrElse ("")

  title_and_version : String = (name + " " + version) .trim

  output_content (content : String) : Boolean =
    SomeSD .mk [String] (content)
      .map (lambda x --> println (x) )
      .map (lambda x --> true)
      .getOrElse (false)

end

class Help
  extends
    AbstractHelp

  file_name = "help.txt"

  execute : Seq [String] -> Boolean =
    lambda arguments -->
      output_content (title_and_version + "\n\n" + read (file_name) )

end

class Manual
  extends
    AbstractHelp

  file_name = "Manual.soda"

  execute : Seq [String] -> Boolean =
    lambda arguments -->
      output_content ("/* " + title_and_version + " */\n\n" + read (file_name) )

end

class License
  extends
    AbstractHelp

  license_file_name = "soda-license.txt"

  notice_file_name = "soda-notice.txt"

  _separator = "\n\n---\n\n"

  execute : Seq [String] -> Boolean =
    lambda arguments -->
      output_content (
        read (notice_file_name) +
        _separator +
        read (license_file_name)
      )

end

