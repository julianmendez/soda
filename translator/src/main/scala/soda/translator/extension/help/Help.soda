package soda.translator.extension.help

class AbstractHelp
  extends
    soda.translator.extension.common.Extension

  abstract
    execute : Seq [String] -> Boolean

  import
    soda.lib.SomeSD_
    soda.translator.io.SimpleFileReader_

  path : String = "/soda/translator/documentation/"

  read (file_name : String) : String =
    SimpleFileReader_ ().read_resource (path + file_name)

  this_package = this.getClass.getPackage

  name : String = Option (this_package.getImplementationTitle).getOrElse ("")

  version : String = Option (this_package.getImplementationVersion).getOrElse ("")

  title_and_version : String = (name + " " + version).trim

  output_content (content : String) : Boolean =
    SomeSD_ (content)
      .map ( lambda x --> println (x) )
      .map ( lambda x --> true)
      .getOrElse (false)

end

class Help
  extends
    AbstractHelp

  file_name = "help.txt"

  execute : Seq [String] -> Boolean =
    lambda arguments -->
      output_content (title_and_version + "\n\n" + read (file_name) )

end

class Manual
  extends
    AbstractHelp

  file_name = "Manual.soda"

  execute : Seq [String] -> Boolean =
    lambda arguments -->
      output_content ("/* " + title_and_version + " */\n\n" + read (file_name) )

end

class License
  extends
    AbstractHelp

  file_name = "LICENSE.txt"

  execute : Seq [String] -> Boolean =
    lambda arguments -->
      output_content (read (file_name))

end

