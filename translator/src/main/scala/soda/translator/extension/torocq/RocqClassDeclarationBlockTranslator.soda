
class RocqClassDeclarationBlockTranslator
  extends
    soda.translator.block.BlockTranslator

  abstract

  import
    soda.translator.block.AnnotatedBlock
    soda.translator.block.Block
    soda.translator.block.Translator
    soda.translator.blocktr.TableTranslator_
    soda.translator.parser.BlockBuilder
    soda.translator.parser.SodaConstant
    soda.translator.parser.annotation.ClassBeginningAnnotation
    soda.translator.parser.annotation.ClassBeginningAnnotation_
    soda.translator.replacement.Replacement_

  _sc = SodaConstant .mk

  _tc = TranslationConstantToRocq .mk

  soda_space : String = _sc .space

  scala_space : String = " "

  get_first_line (lines : Seq [String] ) : String =
    lines .headOption .getOrElse ("")

  get_initial_spaces_for (line : String) : String =
    line .takeWhile (lambda ch --> ch .isSpaceChar)

  get_initial_spaces (lines : Seq [String] ) : String =
    get_initial_spaces_for (get_first_line (lines) )

  _process_after_extends (lines : Seq [String] ) : Seq [String] =
    if (get_first_line (lines) .trim .nonEmpty)
    then lines .map (lambda line -->
       _tc .rocq_import_reserved_word + _tc .rocq_space + line .trim +
       _tc .rocq_space + _tc .rocq_end_symbol)
    else Seq [String] ()

  remove_first_line (lines : Seq [String] ) : Seq [String] =
    if lines .isEmpty
    then lines
    else lines .tail

  remove_first_line (block : Block) : Block =
    BlockBuilder .mk .build (remove_first_line (block .lines) )

  _process_if_extends (lines : Seq [String] ) : Seq [String] =
    if (get_first_line (lines) .trim == _sc .extends_reserved_word)
    then Seq [String] (
      get_initial_spaces (lines) ) .++ (_process_after_extends (remove_first_line (lines) ) )
    else lines

  _process_tail (lines : Seq [String] ) : Seq [String] =
    _process_if_extends (remove_first_line (lines) )

  table_translator : Translator =
    TableTranslator_ (
      Seq (Tuple2 (_sc .class_reserved_word, _tc .rocq_class_reserved_word ) )
    )

  _process_head_with (class_name : String) (line : String) : Seq [String] =
    Seq [String] (
      _tc .rocq_module_reserved_word + _tc .rocq_space + class_name + _tc .rocq_space +
      _tc .rocq_end_symbol + _tc .rocq_new_line + _tc .rocq_new_line +
      Replacement_ (_sc .space + line)
        .replace_at_beginning (0) (table_translator)
        .line .substring (_sc .space .length) +
      _tc .rocq_space + _tc .rocq_type_membership_symbol + _tc .rocq_space +
      _tc .rocq_type_reserved_word + _tc .rocq_space + _tc .rocq_function_definition_symbol
    )

  _process_head (class_name : String) (lines : Seq [String] ) : Seq [String] =
    _process_head_with (class_name) (get_first_line (lines) )

  contains_equals (line : String) : Boolean =
    line .trim .contains (_sc .function_definition_symbol)

  has_condition_for_type_alias (line : String) : Boolean =
    contains_equals (line)

  _translate_block (block : ClassBeginningAnnotation) : Block =
    if (has_condition_for_type_alias (get_first_line (block .lines) ) )
    then block
    else
      BlockBuilder .mk .build (
        _process_head (block .class_name) (block .lines) ++ _process_tail (block .lines)
      )

  _translate_class_beginning_block (block : ClassBeginningAnnotation)
      : ClassBeginningAnnotation =
    ClassBeginningAnnotation_ (_translate_block (block) )

  translate_for (annotated_block : AnnotatedBlock) : AnnotatedBlock =
    match annotated_block
      case ClassBeginningAnnotation_ (block) ==>
        _translate_class_beginning_block (ClassBeginningAnnotation_ (block) )
      case _otherwise ==> annotated_block

  translate : AnnotatedBlock -> AnnotatedBlock =
    lambda block -->
      translate_for (block)

end

