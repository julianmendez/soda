
class ScalaFunctionDefinitionBlockTranslator
  extends
    soda.translator.block.BlockTranslator

  abstract

  import
    soda.translator.block.AnnotatedBlock
    soda.translator.block.AnnotatedLine
    soda.translator.block.AnnotatedLine_
    soda.translator.block.Block
    soda.translator.parser.BlockBuilder
    soda.translator.parser.SodaConstant
    soda.translator.parser.annotation.FunctionDefinitionAnnotation
    soda.translator.parser.annotation.FunctionDefinitionAnnotation_
    soda.translator.parser.tool.CommentDelimiterRemover
    soda.translator.parser.tool.FunctionDefinitionLineDetector
    soda.translator.parser.tool.FunctionDefinitionTypeEnum
    soda.translator.parser.tool.FunctionDefinitionTypeId
    soda.translator.replacement.Replacement

  _sc = SodaConstant .mk

  _tc = TranslationConstantToScala .mk

  _fc = FunctionDefinitionTypeEnum .mk

  _cc = CommentDelimiterRemover .mk

  _soda_def_prefix = _sc .def_reserved_word + _sc .space

  _empty_string = ""

  _prepend_line (line : String) (block : Block) : Block =
    BlockBuilder .mk .build (
      Seq [String] (line) .++ (block .lines)
    )

  _is_annotation (line : String) : Boolean =
    (line .trim == _sc .tail_recursion_annotation) or (line .trim == _sc .override_annotation)

  _private_prefix_if_necessary (line : String) : String =
    if line .trim .startsWith (_sc .private_function_prefix)
    then _tc .scala_private_reserved_word + _tc .scala_space
    else _empty_string

  _translate_val_definition (line : String) : String =
    Replacement .mk (line)
      .add_after_spaces_or_pattern (_tc .scala_space) (_private_prefix_if_necessary (line) +
        _tc .scala_value + _tc .scala_space)
      .line

  _translate_def_definition (line : String) : String =
    Replacement .mk (line)
      .add_after_spaces_or_pattern (_tc .scala_space) (_private_prefix_if_necessary (line) +
        _tc .scala_definition + _tc .scala_space)
      .line

  _replace_first_line (lines : Seq [AnnotatedLine] ) (new_first_line : String)
      : Seq [AnnotatedLine] =
    if lines .isEmpty
    then Seq [AnnotatedLine] () .+: (AnnotatedLine_ (new_first_line , false) )
    else lines .tail .+: (AnnotatedLine_ (new_first_line , false) )


  _replace_on_val_block (initial_comments : Seq [AnnotatedLine] )
      (main_block : Seq [AnnotatedLine] ) : Block =
    Block .mk (
      initial_comments .++ (_replace_first_line (main_block) (
        _translate_val_definition (main_block .head .line) ) )
    )

  _replace_on_def_block (initial_comments : Seq [AnnotatedLine] )
      (main_block : Seq [AnnotatedLine] ) : Block =
    Block .mk (
      initial_comments .++ (_replace_first_line (main_block) (
        _translate_def_definition (main_block .head .line) ) )
    )

  _get_initial_comment (lines : Seq [AnnotatedLine] ) : Seq [AnnotatedLine] =
    lines .takeWhile (lambda annotated_line --> annotated_line .is_comment)

  _get_part_without_initial_comment (lines : Seq [AnnotatedLine] ) : Seq [AnnotatedLine] =
    lines .dropWhile (lambda annotated_line --> annotated_line .is_comment)

  get_first_line (lines : Seq [String] ) : String =
    lines .headOption .getOrElse (_empty_string)

  remove_first_line (lines : Seq [String] ) : Seq [String] =
    if lines .isEmpty
    then lines
    else lines .tail

  _remove_type_annotation_in_line (lines : Seq [String] ) : Seq [String] =
    Seq [String] (
      get_first_line (lines)
        .replaceAll (_sc .main_type_membership_regex , _empty_string)
    )

  remove_type_annotation (lines : Seq [String] ) : Seq [String] =
    _remove_type_annotation_in_line (lines) ++ remove_first_line (lines)

  _translate_main_block_with (block : Block) (detector : FunctionDefinitionLineDetector)
      : Block =
    match detector .detect
      case _fc .val_detected ==>
        _replace_on_val_block (_get_initial_comment (block .annotated_lines) ) (
          _get_part_without_initial_comment (block .annotated_lines) )
      case _fc .def_detected ==>
        _replace_on_def_block (_get_initial_comment (block .annotated_lines) ) (
          _get_part_without_initial_comment (block .annotated_lines) )
      case _fc .def_reserved_word_detected ==> block
      case _fc .undetected ==> block
      case _otherwise ==> block

  _flatten_block (block : Block) : String =
    block .lines .mkString (_sc .space)

  _translate_main_block (block : Block) : Block =
    BlockBuilder .mk .build (
      remove_type_annotation (
        _translate_main_block_with (block) (
            FunctionDefinitionLineDetector .mk (_flatten_block (block) ) )
          .lines
      )
    )

  _remove_def_if_present (block : Block) : Block =
    if (block .lines .nonEmpty) and
      (block .lines .head .trim .startsWith (_soda_def_prefix) )
    then
      BlockBuilder .mk .build (
      (Seq [String] ()
        .+: (_cc .remove_part (block .lines .head) (_soda_def_prefix) ) )
        .++ (block .lines .tail)
      )
    else block

  _remove_first_line_if_possible (block : Block) : Block =
    if block .lines .isEmpty
    then block
    else BlockBuilder .mk .build (block .lines .tail)

  _translate_block_with (first_line : AnnotatedLine) (block : Block) : Block =
    if _is_annotation (first_line .line)
    then _prepend_line (first_line .line) (_translate_main_block (
      _remove_first_line_if_possible (block) ) )
    else _translate_main_block (
      _remove_def_if_present (block) )

  _translate_block (block : Block) : Block =
    if block .readable_lines .isEmpty
    then block
    else _translate_block_with (block .readable_lines .head) (block)

  _translate_function_definition_block (block : Block) : FunctionDefinitionAnnotation =
    FunctionDefinitionAnnotation .mk (_translate_block (block) )

  translate_for (annotated_block : AnnotatedBlock) : AnnotatedBlock =
    match annotated_block
      case FunctionDefinitionAnnotation_ (block) ==>
        _translate_function_definition_block (block)
      case _otherwise ==> annotated_block

  translate : AnnotatedBlock -> AnnotatedBlock =
    lambda block -->
      translate_for (block)

end

