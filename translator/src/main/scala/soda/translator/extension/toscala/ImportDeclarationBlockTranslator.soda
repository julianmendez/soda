package soda.translator.extension.toscala

* ImportDeclarationBlockTranslator
  extends soda.translator.block.BlockTranslator =

  import
    soda.translator.block.AnnotatedBlock
    soda.translator.block.BlockAnnotationEnum_
    soda.translator.parser.BlockBuilder_

  space = " "

  tc = TranslationConstantToScala_ ()

  scala_import_declaration_pattern =
    tc.scala_import_declaration + space

  _labels = BlockAnnotationEnum_ ()

  translate (block: AnnotatedBlock): AnnotatedBlock =
    if block.block_annotation == _labels.import_declaration
    then _translate_block (block)
    else block

  _translate_block (block: AnnotatedBlock): AnnotatedBlock =
    if is_import_block_declaration (block)
    then prepend_to_lines_aligned_at (
      get_number_of_spaces_at_beginning (get_first_line (block) ),
      scala_import_declaration_pattern,
      remove_first_line (block) )
    else block

  prepend_to_lines_aligned_at (number_of_spaces: Int, prefix: String, block: AnnotatedBlock): AnnotatedBlock =
    BlockBuilder_ ().build (
      block.lines.map (line -> prepend_aligned_at (number_of_spaces, prefix, line) ),
      block.block_annotation
    )

  prepend_aligned_at (index: Int, prefix: String, line: String): String =
    line.substring (0, index) + prefix + line.substring (index)

  get_number_of_spaces_at_beginning (line: String): Int =
    line
      .takeWhile (ch => ch.isSpaceChar)
      .length

  remove_first_line (block: AnnotatedBlock): AnnotatedBlock =
    BlockBuilder_ ().build (
      ( if block.lines.isEmpty
        then block.lines
        else block.lines.tail ),
      block.block_annotation
    )

  get_first_line (block: AnnotatedBlock): String =
    block.lines.headOption.getOrElse ("")

  is_import_block_declaration (block: AnnotatedBlock): Boolean =
    (get_first_line (block) ).trim == tc.import_reserved_word

end

* ImportDeclarationBlockTranslator_ ()
  extends ImportDeclarationBlockTranslator

