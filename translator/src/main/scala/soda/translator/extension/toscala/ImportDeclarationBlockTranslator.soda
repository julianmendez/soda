package soda.translator.extension.toscala

class ImportDeclarationBlockTranslator
  extends
    soda.translator.block.BlockTranslator

  import
    soda.translator.block.AnnotatedBlock
    soda.translator.block.AnnotatedLine
    soda.translator.block.BlockAnnotationEnum_
    soda.translator.block.BlockAnnotationId
    soda.translator.parser.BlockBuilder_
    soda.translator.parser.annotation.ImportDeclarationAnnotation
    soda.translator.parser.annotation.ImportDeclarationAnnotation_

  space = " "

  tc = TranslationConstantToScala_ ()

  scala_import_declaration_pattern =
    tc.scala_import_declaration + space

  _labels = BlockAnnotationEnum_ ()

  translate: AnnotatedBlock -> AnnotatedBlock =
    lambda block ->
      translate_for (block)

  translate_for (annotated_block: AnnotatedBlock): AnnotatedBlock =
    match annotated_block
      | block: ImportDeclarationAnnotation => _translate_block (block)
      | x => annotated_block
    end

  _translate_block (block: ImportDeclarationAnnotation): AnnotatedBlock =
    if is_import_block_declaration (block)
    then prepend_to_lines_aligned_at (
      get_number_of_spaces_at_beginning (get_first_line (block) ),
      scala_import_declaration_pattern,
      ImportDeclarationAnnotation_ (block).imported_items,
      block.block_annotation )
    else block

  prepend_to_lines_aligned_at (number_of_spaces: Int, prefix: String, annotated_lines: Seq [AnnotatedLine], block_annotation: BlockAnnotationId): AnnotatedBlock =
    BlockBuilder_ ().build (
      annotated_lines.map (annotated_line -> prepend_aligned_non_comment (number_of_spaces, prefix, annotated_line) ),
      block_annotation
    )

  prepend_aligned_non_comment (index: Int, prefix: String, annotated_line: AnnotatedLine): String =
    if annotated_line.is_comment
    then annotated_line.line
    else annotated_line.line.substring (0, index) + prefix + annotated_line.line.substring (index)

  get_number_of_spaces_at_beginning (line: String): Int =
    line
      .takeWhile (ch => ch.isSpaceChar)
      .length

  get_first_line (block: AnnotatedBlock): String =
    block.lines.headOption.getOrElse ("")

  is_import_block_declaration (block: AnnotatedBlock): Boolean =
    (get_first_line (block) ).trim == tc.import_reserved_word

end

class ImportDeclarationBlockTranslator_ ()
  extends
    ImportDeclarationBlockTranslator

end

