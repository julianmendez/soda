package soda.translator.extension.toscala

class ClassDeclarationBlockTranslator
  extends
    soda.translator.block.BlockTranslator

  import
    soda.translator.block.AnnotatedBlock
    soda.translator.block.Block
    soda.translator.block.Translator
    soda.translator.blocktr.TableTranslator_
    soda.translator.parser.BlockBuilder_
    soda.translator.parser.SodaConstant_
    soda.translator.parser.annotation.ClassBeginningAnnotation
    soda.translator.parser.annotation.ClassBeginningAnnotation_
    soda.translator.parser.annotation.ClassAliasAnnotation
    soda.translator.parser.annotation.ClassAliasAnnotation_
    soda.translator.replacement.Replacement_

  sc = SodaConstant_ ()

  tc = TranslationConstantToScala_ ()

  soda_space : String = sc.space

  scala_space : String = " "

  translate : AnnotatedBlock -> AnnotatedBlock =
    lambda block -->
      translate_for (block)

  translate_for (annotated_block : AnnotatedBlock) : AnnotatedBlock =
    match annotated_block
      case block : ClassBeginningAnnotation => _translate_class_beginning_block (block)
      case block : ClassAliasAnnotation => _translate_class_alias_block (block)
      case x => annotated_block
    end

  _translate_class_beginning_block (block : ClassBeginningAnnotation) : ClassBeginningAnnotation =
    ClassBeginningAnnotation_ (_translate_block (block) )

  _translate_class_alias_block (block : ClassAliasAnnotation) : ClassAliasAnnotation =
    ClassAliasAnnotation_ (_translate_block (block) )

  _translate_block (block : AnnotatedBlock) : Block =
    BlockBuilder_ ().build (
      if (has_condition_for_type_alias (get_first_line (block) ) )
      then _process_head (block) ++ _process_tail (block)
      else _process_head (block) ++ _process_tail (block) ++  Seq [String] (tc.scala_class_begin_symbol)
    )

  _process_head (block : Block) : Seq [String] =
    _process_head_with (get_first_line (block), block)

  _process_head_with (line : String, block : Block) : Seq [String] =
    Seq [String] (Replacement_ (sc.space + line).replace_at_beginning (0) (get_table_translator (line) ).line.substring (sc.space.length) )

  _process_tail (block : Block) : Seq [String] =
    _process_if_extends (remove_first_line (block) )

  _process_if_extends (block : Block) : Seq [String] =
    if (get_first_line (block).trim == sc.extends_reserved_word)
    then Seq [String] (get_spaces_at_beginning (get_first_line (block) ) + tc.scala_extends_translation) ++ _process_after_extends (remove_first_line (block) )
    else block.lines

  get_table_translator (line : String) : Translator =
    TableTranslator_ (
      Seq (Tuple2 (sc.class_reserved_word, get_class_declaration_translation (line) ) )
    )

  get_class_declaration_translation (line : String) : String =
    if line.contains (sc.opening_parenthesis_symbol)
    then tc.class_declaration_translation_at_beginning_with_paren
    else
      if has_condition_for_type_alias (line)
      then tc.class_declaration_translation_at_beginning_without_paren_for_type_alias
      else tc.class_declaration_translation_at_beginning_without_paren

  get_number_of_spaces_at_beginning (line : String) : Int =
    line
      .takeWhile (lambda ch -> ch.isSpaceChar)
      .length

  get_spaces_at_beginning (line : String) : String =
    line.substring (0, get_number_of_spaces_at_beginning (line) )

  _process_after_extends (block : Block) : Seq [String] =
    if (get_first_line (block).trim.nonEmpty)
    then Seq [String] (get_first_line (block)) ++ remove_first_line (block).lines.map (lambda line -> get_spaces_at_beginning (line) + tc.scala_with_translation + scala_space + line.trim)
    else Seq [String] ()

  remove_first_line (block : Block) : Block =
    BlockBuilder_ ().build (
      if block.lines.isEmpty
      then block.lines
      else block.lines.tail
    )

  get_first_line (block : Block) : String =
    block.lines.headOption.getOrElse ("")

  ends_with_equals (line : String) : Boolean =
    line.trim.endsWith (sc.deprecated_class_definition_symbol)

  ends_with_opening_brace (line : String) : Boolean =
    line.trim.endsWith (sc.deprecated_class_beginning_symbol)

  contains_equals (line : String) : Boolean =
    line.trim.contains (sc.function_definition_symbol)

  has_condition_for_type_alias (line : String) : Boolean =
    contains_equals (line) and not (ends_with_equals (line) or ends_with_opening_brace (line) )

end

