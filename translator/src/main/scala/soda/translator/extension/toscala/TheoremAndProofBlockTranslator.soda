package soda.translator.extension.toscala

class TheoremAndProofBlockTranslator
  extends
    soda.translator.block.BlockTranslator

  import
    soda.translator.block.AnnotatedBlock
    soda.translator.block.BlockAnnotationEnum_
    soda.translator.parser.BlockBuilder_
    soda.translator.parser.annotation.ProofBlockAnnotation
    soda.translator.parser.annotation.TheoremBlockAnnotation

  tc = TranslationConstantToScala_ ()

  _labels = BlockAnnotationEnum_ ()

  translate: AnnotatedBlock -> AnnotatedBlock =
    lambda block ->
      translate_for (block)

  translate_for (annotated_block: AnnotatedBlock): AnnotatedBlock =
    match annotated_block
      | block: TheoremBlockAnnotation => _translate_block (block)
      | block: ProofBlockAnnotation => _translate_block (block)
      | x => annotated_block
    end

  _translate_block (block: AnnotatedBlock): AnnotatedBlock =
    if is_a_theorem_or_a_proof (block)
    then append (
      tc.comment_close, prepend (
        tc.comment_open, block ) )
    else block

  prepend (prefix: String, block: AnnotatedBlock): AnnotatedBlock =
    BlockBuilder_ ().build (
      Seq [String] (prefix + block.lines.head) ++ block.lines.tail, block.block_annotation
    )

  append (suffix: String, block: AnnotatedBlock): AnnotatedBlock =
    BlockBuilder_ ().build (
      block.lines.:+ (suffix), block.block_annotation
    )

  first_line (block: AnnotatedBlock): String =
    block.lines.headOption.getOrElse ("").trim

  is_a_theorem_or_a_proof (block: AnnotatedBlock): Boolean =
    _is_line_a_theorem_or_a_proof (first_line (block) )

  _is_line_a_theorem_or_a_proof (line: String): Boolean =
    line == tc.theorem_reserved_word or
    line == tc.proof_reserved_word

end

class TheoremAndProofBlockTranslator_ ()
  extends
    TheoremAndProofBlockTranslator

end

