
class ScalaMainClassBlockTranslator
  extends
    soda.translator.block.BlockTranslator

  abstract

  import
    soda.translator.block.AnnotatedBlock
    soda.translator.parser.BlockBuilder
    soda.translator.parser.annotation.ClassBeginningAnnotation
    soda.translator.parser.annotation.ClassBeginningAnnotation_
    soda.translator.parser.annotation.ClassEndAnnotation
    soda.translator.parser.annotation.ClassEndAnnotation_

  _tc = TranslationConstantToScala .mk

  _get_as_class_beginning_annotation (block : AnnotatedBlock) : Option [ClassBeginningAnnotation] =
    match block
      case ClassBeginningAnnotation_ (b) ==> Some (ClassBeginningAnnotation_ (b) )
      case _otherwise ==> None

  _get_class_beginning (references : Seq [AnnotatedBlock] ) : Option [ClassBeginningAnnotation] =
    references
      .flatMap (lambda block --> _get_as_class_beginning_annotation (block) )
      .headOption

  _get_class_name (references : Seq [AnnotatedBlock] ) : String =
    _get_class_beginning (references)
      .map (lambda x --> x .class_name)
      .getOrElse ("")

  _translate_block (block : ClassEndAnnotation) : ClassEndAnnotation =
    if _get_class_name (block .references) == _tc .soda_main_class_name
    then
      ClassEndAnnotation .mk (
        BlockBuilder .mk .build (
          Seq [String] (
            _tc .scala_class_end_symbol,
            "",
            _tc .scala_entry_point
          )
        ) ) (
        block .references
      )
    else block

  translate_for (annotated_block : AnnotatedBlock) : AnnotatedBlock =
    match annotated_block
      case ClassEndAnnotation_ (block) (references) ==>
        _translate_block (ClassEndAnnotation .mk (block) (references) )
      case _otherwise ==> annotated_block

  translate : AnnotatedBlock -> AnnotatedBlock =
    lambda block -->
      translate_for (block)

end

