
class ScalaMainClassBlockTranslator
  extends
    soda.translator.block.BlockTranslator

  import
    soda.translator.block.AnnotatedBlock
    soda.translator.block.Block
    soda.translator.parser.BlockBuilder_
    soda.translator.parser.annotation.ClassBeginningAnnotation
    soda.translator.parser.annotation.ClassBeginningAnnotation_
    soda.translator.parser.annotation.ClassEndAnnotation
    soda.translator.parser.annotation.ClassEndAnnotation_

  _mk_ClassEndAnnotation (block : Block) (references : Seq [AnnotatedBlock] )
      : ClassEndAnnotation =
    ClassEndAnnotation_ (block) (references)

  _tc = TranslationConstantToScala_ ()

  _get_as_class_beginning_annotation (block : AnnotatedBlock) : Option [ClassBeginningAnnotation] =
    match block
      case ClassBeginningAnnotation_ (b) ==> Some (ClassBeginningAnnotation_ (b) )
      case otherwise ==> None

  _get_class_beginning (references : Seq [AnnotatedBlock] ) : Option [ClassBeginningAnnotation] =
    references
      .flatMap (lambda block --> _get_as_class_beginning_annotation (block) )
      .headOption

  _get_class_name (references : Seq [AnnotatedBlock] ) : String =
    _get_class_beginning (references)
      .map (lambda x --> x .class_name)
      .getOrElse ("")

  _translate_block (block : ClassEndAnnotation) : ClassEndAnnotation =
    if _get_class_name (block .references) == _tc .soda_main_class_name
    then
      _mk_ClassEndAnnotation (
        BlockBuilder_ () .build (
          Seq [String] (
            _tc .scala_class_end_symbol,
            "",
            _tc .scala_entry_point
          )
        ) ) (
        block .references
      )
    else block

  translate_for (annotated_block : AnnotatedBlock) : AnnotatedBlock =
    match annotated_block
      case ClassEndAnnotation_ (block) (references) ==>
        _translate_block (_mk_ClassEndAnnotation (block) (references) )
      case otherwise ==> annotated_block

  translate : AnnotatedBlock -> AnnotatedBlock =
    lambda block -->
      translate_for (block)

end

