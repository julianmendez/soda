
class MatchCaseBlockTranslator
  extends
    soda.translator.block.BlockTranslator

  import
    soda.translator.block.AnnotatedBlock
    soda.translator.block.Block
    soda.translator.parser.BlockBuilder_
    soda.translator.parser.SodaConstant_
    soda.translator.parser.annotation.FunctionDefinitionAnnotation
    soda.translator.parser.annotation.FunctionDefinitionAnnotation_
    soda.translator.parser.annotation.TestDeclarationAnnotation
    soda.translator.parser.annotation.TestDeclarationAnnotation_

  _sc = SodaConstant_ ()

  _tc = TranslationConstantToScala_ ()

  _soda_match_pattern = _sc.match_reserved_word + " "

  translate: AnnotatedBlock -> AnnotatedBlock =
    lambda block -->
      translate_for (block)

  translate_for (annotated_block : AnnotatedBlock) : AnnotatedBlock =
    match annotated_block
      case FunctionDefinitionAnnotation_ (block) ==> _translate_function_block (FunctionDefinitionAnnotation_ (block) )
      case TestDeclarationAnnotation_ (block) ==> _translate_test_block (TestDeclarationAnnotation_ (block) )
      case x ==> annotated_block

  _translate_function_block (block : AnnotatedBlock) : FunctionDefinitionAnnotation =
    FunctionDefinitionAnnotation_ (_translate_block (block) )

  _translate_test_block (block : AnnotatedBlock) : TestDeclarationAnnotation =
    TestDeclarationAnnotation_ (_translate_block (block) )

  _translate_block (block : AnnotatedBlock) : Block =
    if _is_a_match_case_structure (block)
    then _translate_match_case_structure (block) (_get_tabulation_of_match (block) )
    else block

  _is_a_match_case_structure (block : AnnotatedBlock) : Boolean =
    block.lines.exists ( lambda line --> _is_a_match_line (line) )

  _is_a_match_line (line : String) : Boolean =
    line.trim.startsWith (_soda_match_pattern)

  _get_tabulation_of_match (block : AnnotatedBlock) : String =
    block.lines
      .find ( lambda line --> _is_a_match_line (line) )
      .map ( lambda line --> _left_part (line.indexOf (_soda_match_pattern) ) (line) )
      .getOrElse (_tc.scala_space)

  _translate_match_case_structure (block: AnnotatedBlock) (tabulation : String) : Block =
    BlockBuilder_ ().build (
      block.lines
        .map ( lambda line --> _insert_match_before_brace_if_found (line) )
        .++ ( Seq [String] () .+: (tabulation + _tc.scala_match_end_translation) )
    )

  _insert_match_before_brace_if_found (line : String) : String =
    if _is_a_match_line (line)
    then _assemble_parts (index := line.indexOf (_soda_match_pattern) ) (line)
    else line

  _assemble_parts (index : Int) (line : String) : String =
    (_left_part (index) (line) ) + (_right_part (index) (line) ) + _tc.scala_match_translation + _tc.scala_space + _tc.scala_opening_brace

  _left_part (index : Int) (line : String) : String =
    line.substring (0, index)

  _right_part (index : Int) (line : String) : String =
    line.substring (index + _soda_match_pattern.length, line.length)

end

