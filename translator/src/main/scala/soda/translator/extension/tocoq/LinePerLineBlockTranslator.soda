package soda.translator.extension.tocoq

* LinePerLineBlockTranslator
  extends soda.translator.block.BlockTranslator = {

  + soda.translator.block.Block
  + soda.translator.blocktr.TableBlockTranslator_
  + soda.translator.blocktr.TokenizedBlockTranslator_
  + soda.translator.replacement.Replacement_
  + soda.translator.replacement.Token

  source = "soda"

  target = "coq"

  tc = TranslationConstantToCoq_ ()

  new_line = "\n"

  space = " "

  soda_opening_parenthesis: String = "("

  synonym_at_beginning = TableBlockTranslator_ (tc.synonym_at_beginning)

  synonym = TableBlockTranslator_ (tc.synonym)

  main_translation = TableBlockTranslator_ (tc.main_translation)

  coq_non_soda = TableBlockTranslator_ (tc.coq_non_soda)

  beautifier = TableBlockTranslator_ (tc.beautifier)

  translator = TokenizedBlockTranslator_ (replace_token)

  translate (block: Block): Block =
    translator.translate (block)

  replace_token: Token -> String =
    lambda token ->
      Replacement_ (token.text)
        .add_spaces_to_symbols (symbols := tc.soda_brackets_and_comma.toSet)
        .replace (coq_non_soda)
        .replace_at_beginning (token.index, synonym_at_beginning)
        .replace (synonym)
        .replace_with (try_definition)
        .replace (main_translation)
        .replace_regex (beautifier)
        .line

  try_definition (line: String): String =
    DefinitionTranslatorToCoq_ (line).translation

}

* LinePerLineBlockTranslator_ ()
  extends LinePerLineBlockTranslator

