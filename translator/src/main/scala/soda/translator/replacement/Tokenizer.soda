package soda.translator.replacement


/**
 * A token is a piece of code, that can contain one or more words combined with symbols.
 */
* Token(text: String, parser_state: ParserState, index: Int) = {

  @override
  toString: String =
    "(\"" + text + "\"," + parser_state + ", " + index + ")"
}

/**
 * This class processes a line to divide it into tokens.
 */
* Tokenizer(line: String) = {
  + soda.lib.Rec

  get_tokens: Seq[Token] =
    _postproc(Rec().foldLeft( Rec().range(line.length), _initial_value, _next_value ))

  _initial_value = FoldTuple(0, ParserStateEnum().Plain, Seq())

  _postproc(tuple: FoldTuple): Seq[Token] =
    (tuple.rev_tokens.+:(Token(line.substring(tuple.last_index), tuple.parser_state, tuple.last_index)))
      .reverse

  _next_value(tuple: FoldTuple, current_index: Int): FoldTuple =
    let
      ch = line.charAt(current_index)
      char_type = CharTypeEnum().get_char_type(ch)
      new_parser_state = ParserTransition().next_parser_state(tuple.parser_state, char_type)
      result =
        if ParserStateEnum().is_same_class(new_parser_state, tuple.parser_state)
        then FoldTuple(tuple.last_index, new_parser_state, tuple.rev_tokens)
        else _next_value_of_different_class(tuple, current_index, new_parser_state)
    in result

  _next_value_of_different_class(tuple: FoldTuple, current_index: Int,
      new_parser_state: ParserState): FoldTuple =
    let
      index =
        if tuple.parser_state == ParserStateEnum().QuotesState or
           tuple.parser_state == ParserStateEnum().ApostropheState
        then current_index + 1
        else current_index
      text = line.substring(tuple.last_index, index)

    in FoldTuple(index, new_parser_state,
        tuple.rev_tokens.+:(Token(text, tuple.parser_state, tuple.last_index)))

  * FoldTuple(last_index: Int, parser_state: ParserState, rev_tokens: Seq[Token])
}

