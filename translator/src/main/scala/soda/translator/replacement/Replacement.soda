package soda.translator.replacement


/**
 * This models a collection of replacement functions.
 * This is intended to be used as a pipeline.
 */
* Replacement(line: String) = {
  + soda.lib.Rec

  SodaSpace: String = " "
  ScalaSpace: String = " "

  replace_with(function: String -> String): Replacement =
    Replacement(function(line))

  replace_at_beginning(index: Int, translator: Translator): Replacement =
    Replacement(replace_at_beginning(line, index, translator))

  replace_at_beginning(line: String, index: Int, translator: Translator): String =
    if index == 0
    then replace_only_beginning(line, translator)
    else line

  replace_only_beginning(line: String, translator: Translator): String =
    let
      initial_value: String = line
      next_value(line: String, reserved_word: String): String =
        _replace_if_found(line,
          SodaSpace + reserved_word + SodaSpace, ScalaSpace + translator.translate(reserved_word) + ScalaSpace)

    in Rec().foldLeft(translator.keys, initial_value, next_value)

  _replace_if_found(line: String, pattern: String, new_text: String): String =
    if line.trim.startsWith(pattern.trim)
    then replace_all(line, pattern, new_text)
    else line

  replace_all(pattern: String, replacement: String): Replacement =
    Replacement(replace_all(line, pattern, replacement))

  replace_all(line: String, pattern: String, replacement: String): String =
    Replacer(line, pattern, replacement).replace

  replace(translator: Translator): Replacement =
    Replacement(replace(line, translator))

  replace(line: String, translator: Translator): String =
    let
      result = Rec().foldLeft(translator.keys, initial_value, next_value)

      initial_value: String = line

      next_value(line: String, reserved_word: String): String =
        replace_if_found(line,
          SodaSpace + reserved_word + SodaSpace, ScalaSpace + translator.translate(reserved_word) + ScalaSpace)

      replace_if_found(line: String, pattern: String, new_text: String): String =
        if line.contains(pattern)
        then replace_all(line, pattern, new_text)
        else line

    in result

  add_space_to_soda_line(): Replacement =
    Replacement(SodaSpace + line + SodaSpace)

  add_spaces_to_symbols(symbols: Set[Char]): Replacement =
    Replacement(add_spaces_to_symbols(line, symbols))

  add_spaces_to_symbols(line: String, symbols: Set[Char]): String =
    line.indices.map(index ->
      let
        ch = line(index)
        left_part =
          if (index > 0) and symbols.contains(ch) and
            not line(index - 1).isWhitespace
          then ScalaSpace
          else ""
        right_part =
          if (index < line.length - 1) and symbols.contains(ch) and
            not line(index + 1).isWhitespace
          then ScalaSpace
          else ""

      in left_part + ch + right_part
    ).mkString("")

  remove_space_from_scala_line(): Replacement =
    Replacement(remove_space_from_scala_line(line))

  remove_space_from_scala_line(line: String): String =
    let
      line_without_starting_space =
        if line.startsWith(ScalaSpace)
        then line.substring(1)
        else line
      line_without_ending_space =
        if line_without_starting_space.endsWith(ScalaSpace)
        then line_without_starting_space.substring(0, line_without_starting_space.length - 1)
        else line_without_starting_space
    in line_without_ending_space

  add_after_spaces(text_to_prepend: String): Replacement =
    Replacement(add_after_spaces(line, text_to_prepend))

  add_after_spaces(line: String, text_to_prepend: String): String =
    let
      prefix_length = line.takeWhile(ch -> ch.isSpaceChar).length
    in line.substring(0, prefix_length) + text_to_prepend + line.substring(prefix_length)

  replace_regex(translator: Translator): Replacement =
    Replacement(replace_regex(line, translator))

  replace_regex(line: String, translator: Translator): String =
    let
      initial_value: String = line
      next_value(line: String, regex: String): String =
        line.replaceAll(regex, translator.translate(regex))
    in Rec().foldLeft(translator.keys, initial_value, next_value)
}

* Replacer(line: String, pattern: String, replacement: String) = {
  + soda.lib.Rec

  replace =
    postproc( Rec().foldLeftWhile( Rec().range(line.length), initial_value, next_value, condition ) )

  initial_value = ReplacerFoldTuple( Seq(), 0 )

  next_value(tuple: ReplacerFoldTuple, x: Int): ReplacerFoldTuple =
    _next_tuple(
      replaced_text_rev := tuple.replaced_text_rev,
      start_index := tuple.start_index,
      pos := line.indexOf(pattern, tuple.start_index)
    )

  _next_tuple(replaced_text_rev: Seq[String], start_index: Int, pos: Int): ReplacerFoldTuple =
    if pos == -1
    then ReplacerFoldTuple( replaced_text_rev.+:(line.substring(start_index)), pos )
    else
      let
        new_replaced_text_rev = (replaced_text_rev.+:(line.substring(start_index, pos))).+:(replacement)
        new_index = pos + pattern.length
      in ReplacerFoldTuple( new_replaced_text_rev, new_index )

  condition(tuple: ReplacerFoldTuple, x: Int): Boolean =
    not (tuple.start_index == -1)

  postproc(tuple: ReplacerFoldTuple): String =
    tuple.replaced_text_rev.reverse.mkString("")
}

* ReplacerFoldTuple(replaced_text_rev: Seq[String], start_index: Int)

