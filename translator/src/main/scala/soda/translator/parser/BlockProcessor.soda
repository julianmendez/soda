package soda.translator.parser

* BlockProcessor = {

  + soda.lib.SomeSD_
  + soda.translator.block.AnnotatedBlock
  + soda.translator.block.AnnotatedBlock_
  + soda.translator.block.BlockTranslator
  + soda.translator.block.Block
  + soda.translator.block.BlockAnnotationEnum_
  + soda.translator.parser.BlockBuilder_

  has translator: BlockTranslator

  new_line = "\n"

  double_new_line = new_line + new_line

  translate (program: String): String =
    SomeSD_ (program)
      .map (split_blocks)
      .map (annotate_blocks)
      .map (translate_blocks)
      .map (join_translated_blocks)
      .value

  split_blocks (program: String): Seq [Block] =
    program
      .split (double_new_line)
      .toIndexedSeq
      .map (paragraph -> make_block (paragraph) )

  annotate_blocks (blocks: Seq [Block]): Seq [AnnotatedBlock] =
    blocks.map (block -> annotate_block (block))

  annotate_block (block: Block): AnnotatedBlock =
    AnnotatedBlock_ (block.lines, block.annotated_lines, BlockAnnotationEnum_ ().undefined)

  make_block (paragraph: String): Block =
    BlockBuilder_ ().build(
      paragraph
        .split (new_line)
        .toIndexedSeq
    )

  translate_blocks (blocks: Seq [Block]): Seq [Block] =
    blocks.map (block -> translator.translate (block) )

  join_translated_blocks (blocks: Seq [Block]): String =
    blocks
      .map (x -> x.contents)
      .mkString (double_new_line) + new_line

}

* BlockProcessor_ (translator: soda.translator.block.BlockTranslator)
  extends BlockProcessor

