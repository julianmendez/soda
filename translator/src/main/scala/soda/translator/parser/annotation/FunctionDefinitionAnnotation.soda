
class FunctionDefinitionAnnotation
  extends
    BlockAnnotationParser

  abstract
    block : soda.translator.block.Block

  import
    soda.translator.block.BlockAnnotationEnum_
    soda.translator.parser.SodaConstant_
    soda.translator.replacement.ParserStateEnum_
    soda.translator.replacement.Tokenizer_

  identifier = BlockAnnotationEnum_ ().function_definition

  sc = SodaConstant_ ()

  _symbol_at_the_end : String =
    sc.space +
    sc.function_definition_symbol

  _symbol_in_the_middle : String =
    sc.space +
    sc.function_definition_symbol +
    sc.space

  _plain_state = ParserStateEnum_ ().plain

  applies : Boolean =
    not is_a_theorem and
    not is_a_proof and
    not is_a_class_declaration and
    (contains_the_equals_symbol or _starts_with_valid_annotation)

  contains_the_equals_symbol : Boolean =
    block.readable_lines.nonEmpty and
    block.readable_lines
      .filter ( lambda annotated_line --> not annotated_line.is_comment)
      .exists ( lambda annotated_line --> _contains_the_equals_symbol_in_line (annotated_line.line) )

  _contains_the_equals_symbol_in_line (line : String) : Boolean =
    Tokenizer_ (line)
      .tokens
      .exists ( lambda token -->
        token.parser_state == _plain_state and
        _contains_the_equals_symbol_in_token (token.text)
      )

  _contains_the_equals_symbol_in_token (token_text : String) : Boolean =
    (
      (token_text.contains (_symbol_in_the_middle) ) or
      (token_text.endsWith (_symbol_at_the_end) )
    )

  _starts_with_valid_annotation : Boolean =
    block.readable_lines.nonEmpty and
    _starts_with_valid_annotation_with (block.readable_lines.head.line.trim)

  _starts_with_valid_annotation_with (first_line_trimmed : String) : Boolean =
    ( first_line_trimmed == sc.tail_recursion_annotation or
      first_line_trimmed == sc.override_annotation )

  is_a_class_declaration : Boolean =
    starts_with_prefix_and_space (sc.class_reserved_word)

  is_a_theorem : Boolean =
    block.readable_lines.nonEmpty and
    (block.readable_lines.head.line.trim == SodaConstant_ ().theorem_reserved_word)

  is_a_proof : Boolean =
    block.readable_lines.nonEmpty and
    (block.readable_lines.head.line.trim == SodaConstant_ ().proof_reserved_word)

end

