
class AnnotationFactory
  extends
    soda.translator.block.BlockTranslator

  import
    soda.translator.block.AnnotatedBlock
    soda.translator.block.AnnotatedBlock_
    soda.translator.block.Block
    soda.translator.block.BlockAnnotationEnum_

  translate : AnnotatedBlock -> AnnotatedBlock =
    lambda block -->
      translate_for (block)

  translate_for (block : AnnotatedBlock) : AnnotatedBlock =
    if block.block_annotation == BlockAnnotationEnum_ ().undefined
    then annotate (block)
    else block

  annotate (block : Block) : AnnotatedBlock =
    match block
      case AnnotatedBlock_ (annotated_lines, block_annotation) ==> AnnotatedBlock_ (annotated_lines, block_annotation)
      case x ==> _get_first_or_undefined (_find_candidates (x) ) (x)

  update_block (original_content : AnnotatedBlock) (new_content : Block) : AnnotatedBlock =
    match original_content
      case FunctionDefinitionAnnotation_ (b) ==> FunctionDefinitionAnnotation_ (new_content)
      case ClassBeginningAnnotation_ (b) ==> ClassBeginningAnnotation_ (new_content)
      case ClassEndAnnotation_ (b, references) ==> ClassEndAnnotation_ (new_content, references)
      case AbstractDeclarationAnnotation_ (b, references) ==> AbstractDeclarationAnnotation_ (new_content, references)
      case ImportDeclarationAnnotation_ (b) ==> ImportDeclarationAnnotation_ (new_content)
      case PackageDeclarationAnnotation_ (b) ==> PackageDeclarationAnnotation_ (new_content)
      case ClassAliasAnnotation_ (b) ==> ClassAliasAnnotation_ (new_content)
      case TheoremBlockAnnotation_ (b) ==> TheoremBlockAnnotation_ (new_content)
      case ProofBlockAnnotation_ (b) ==> ProofBlockAnnotation_ (new_content)
      case CommentAnnotation_ (b) ==> CommentAnnotation_ (new_content)
      case TestDeclarationAnnotation_ (b) ==> TestDeclarationAnnotation_ (new_content)
      case x ==> AnnotatedBlock_ (new_content.annotated_lines, x.block_annotation)

  _detectors (block : Block) : Seq [BlockAnnotationParser] =
    Seq (
      FunctionDefinitionAnnotation_ (block),
      ClassBeginningAnnotation_ (block),
      ClassEndAnnotation_ (block, Seq [BlockAnnotationParser] () ),
      AbstractDeclarationAnnotation_ (block, Seq [BlockAnnotationParser] () ),
      ImportDeclarationAnnotation_ (block),
      PackageDeclarationAnnotation_ (block),
      ClassAliasAnnotation_ (block),
      TheoremBlockAnnotation_ (block),
      ProofBlockAnnotation_ (block),
      CommentAnnotation_ (block),
      TestDeclarationAnnotation_ (block)
    )

  _find_candidates (block : Block) : Seq [BlockAnnotationParser] =
    _detectors (block)
      .filter (lambda detector --> detector.applies)

  _get_first_or_undefined (candidates : Seq [BlockAnnotationParser] ) (block : Block) : AnnotatedBlock =
    if candidates.length == 1
    then candidates.head
    else AnnotatedBlock_ (block.annotated_lines, BlockAnnotationEnum_ ().undefined )

end

