
class PreprocessorFoldTuple

  abstract
    comment_state : Boolean
    annotated_lines_rev : Seq [soda.translator.block.AnnotatedLine]

end

class CurrentAndNewCommentState

  abstract
    current_state : Boolean
    new_comment_state : Boolean

end

class BlockBuilder

  import
    soda.lib.Fold_
    soda.translator.block.AnnotatedLine
    soda.translator.block.AnnotatedLine_
    soda.translator.block.Block
    soda.translator.block.Block_

  _mk_PreprocessorFoldTuple (comment_state : Boolean)
      (annotated_lines_rev : Seq [AnnotatedLine] ) : PreprocessorFoldTuple =
    PreprocessorFoldTuple_ (comment_state) (annotated_lines_rev)

  _mk_CurrentAndNewCommentState (current_state : Boolean) (new_comment_state : Boolean)
      : CurrentAndNewCommentState =
    CurrentAndNewCommentState_ (current_state) (new_comment_state)

  _sc = SodaConstant_ ()

  _fold = Fold_ ()

  _annotate_this_line_considering_opening_symbol (line : String) (comment_state : Boolean)
      : CurrentAndNewCommentState =
    if line .trim .startsWith (_sc .comment_opening_symbol)
    then _mk_CurrentAndNewCommentState (true) (
      not line .trim .endsWith (_sc .comment_closing_symbol) )
    else _mk_CurrentAndNewCommentState (false) (false)

  _annotate_this_line (line : String) (comment_state : Boolean) : CurrentAndNewCommentState =
    if comment_state
    then _mk_CurrentAndNewCommentState (true) (
      not line .trim .endsWith (_sc .comment_closing_symbol) )
    else _annotate_this_line_considering_opening_symbol (line) (comment_state)

  _get_annotated_lines_initial_value  : PreprocessorFoldTuple =
    _mk_PreprocessorFoldTuple (false) (Seq [AnnotatedLine] () )

  _get_annotated_lines_next_value_function_with (t : CurrentAndNewCommentState)
      (pair : PreprocessorFoldTuple) (line : String) : PreprocessorFoldTuple =
    _mk_PreprocessorFoldTuple (t .new_comment_state) (
      pair .annotated_lines_rev .+: (AnnotatedLine_ (line) (t .current_state) ) )

  _get_annotated_lines_next_value_function (pair : PreprocessorFoldTuple) (line : String)
      : PreprocessorFoldTuple =
    _get_annotated_lines_next_value_function_with (
      _annotate_this_line (line) (pair .comment_state) ) (pair) (line)

  _get_annotated_lines (lines : Seq [String] ) : Seq [AnnotatedLine] =
    _fold.apply [String] [PreprocessorFoldTuple] (lines) (_get_annotated_lines_initial_value) (
        _get_annotated_lines_next_value_function)
      .annotated_lines_rev
      .reverse

  build (lines : Seq [String] ) : Block =
    Block_ (
      _get_annotated_lines (lines)
    )

end

