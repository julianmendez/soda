
class PreprocessorFoldTuple

  abstract
    comment_state : Boolean
    annotated_lines_rev : Seq [soda.translator.block.AnnotatedLine]

end

class CurrentAndNewCommentState

  abstract
    current_state : Boolean
    new_comment_state : Boolean

end

class BlockBuilder

  import
    soda.lib.Fold
    soda.translator.block.AnnotatedLine
    soda.translator.block.Block

  _sc = SodaConstant .mk

  _fold = Fold .mk

  _annotate_this_line_considering_opening_symbol (line : String) (comment_state : Boolean)
      : CurrentAndNewCommentState =
    if line .trim .startsWith (_sc .comment_opening_symbol)
    then CurrentAndNewCommentState .mk (true) (
      not line .trim .endsWith (_sc .comment_closing_symbol) )
    else CurrentAndNewCommentState .mk (false) (false)

  _annotate_this_line (line : String) (comment_state : Boolean) : CurrentAndNewCommentState =
    if comment_state
    then CurrentAndNewCommentState .mk (true) (
      not line .trim .endsWith (_sc .comment_closing_symbol) )
    else _annotate_this_line_considering_opening_symbol (line) (comment_state)

  _get_annotated_lines_initial_value  : PreprocessorFoldTuple =
    PreprocessorFoldTuple .mk (false) (Seq [AnnotatedLine] () )

  _get_annotated_lines_next_value_function_with (t : CurrentAndNewCommentState)
      (pair : PreprocessorFoldTuple) (line : String) : PreprocessorFoldTuple =
    PreprocessorFoldTuple .mk (t .new_comment_state) (
      pair .annotated_lines_rev .+: (AnnotatedLine .mk (line) (t .current_state) ) )

  _get_annotated_lines_next_value_function (pair : PreprocessorFoldTuple) (line : String)
      : PreprocessorFoldTuple =
    _get_annotated_lines_next_value_function_with (
      _annotate_this_line (line) (pair .comment_state) ) (pair) (line)

  _get_annotated_lines (lines : Seq [String] ) : Seq [AnnotatedLine] =
    _fold.apply [String] [PreprocessorFoldTuple] (lines) (_get_annotated_lines_initial_value) (
        _get_annotated_lines_next_value_function)
      .annotated_lines_rev
      .reverse

  build (lines : Seq [String] ) : Block =
    Block .mk (
      _get_annotated_lines (lines)
    )

end

