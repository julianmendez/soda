
class SortExample

  abstract
    is_sorted : Seq [Int] -> Boolean

end

class SortExampleWithAt
  extends
    SortExample

  abstract

  is_sorted_for (sequence : Seq [Int] ) : Boolean =
    sequence
      .indices
      .filter (lambda index --> index > 0)
      .forall (lambda index --> sequence .apply (index - 1) <= sequence .apply (index) )

  is_sorted : Seq [Int] -> Boolean =
    lambda sequence --> is_sorted_for (sequence)

end

class SortExampleWithZip
  extends
    SortExample

  abstract

  is_sorted_for (sequence : Seq [Int] ) : Boolean =
    sequence
      .zip (sequence .tail)
      .forall (lambda pair --> (pair ._1 <= pair ._2) )

  is_sorted : Seq [Int] -> Boolean =
    lambda sequence --> is_sorted_for (sequence)

end

class SortAlgorithmExample

  abstract
    sort : Seq [Int] -> Seq [Int]

end

class SortAlgorithmExampleWithFold
  extends
    SortAlgorithmExample

  abstract

  import
    soda.lib.Fold

  _fold = Fold .mk

  _initial_value = Seq [Int] ()

  _next_value_function (current_sequence : Seq [Int] ) (elem : Int) : Seq [Int] =
    insert_sorted (current_sequence) (elem)

  sort_for (sequence : Seq [Int] ) : Seq [Int] =
    if sequence .length < 2
    then sequence
    else _fold .apply [Int] [Seq [Int] ] (sequence) (_initial_value) (_next_value_function)

  sort : Seq [Int] -> Seq [Int] =
    lambda sequence --> sort_for (sequence)

  concatenate (first_part : Seq [Int] ) (middle : Seq [Int] ) (last_part : Seq [Int] )
      : Seq [Int] =
    first_part .++ (middle .++ (last_part) )

  insert_sorted (sequence : Seq [Int] ) (element : Int) : Seq [Int] =
    concatenate (
      first_part := sequence .takeWhile (lambda x --> x < element) ) (
      middle := Seq (element) ) (
      last_part := sequence .dropWhile (lambda x --> x < element)
    )

end

class ConstrainedSortAlgorithm

  abstract

  import
    soda.lib.OptionSD
    soda.lib.SomeSD
    soda.lib.NoneSD

  sort_with (sorted_sequence : Seq [Int] ) : OptionSD [Seq [Int] ] =
    if SortExampleWithZip .mk .is_sorted (sorted_sequence)
    then SomeSD .mk [Seq [Int] ] (sorted_sequence)
    else NoneSD .mk [Seq [Int] ] ()

  sort (sequence : Seq [Int] ) : OptionSD [Seq [Int] ] =
    sort_with (SortAlgorithmExampleWithFold .mk .sort (sequence) )

end

class SortedSequence [A subtype Comparable [A] ]

  abstract
    sequence : Seq [A]
    add : A -> SortedSequence [A]
    invariant : Boolean

end

class EmptySortedSequence [A subtype Comparable [A] ]
  extends
    SortedSequence [A]

  abstract

  sequence = Seq ()

  add_for (element : A) : SortedSequence [A] =
    NonEmptySortedSequence_ (Seq (element) )

  add : A -> SortedSequence [A] =
    lambda element --> add_for (element)

  invariant : Boolean = true

end

class SortedSequenceWithElements [A subtype Comparable [A] ]
  extends
    SortedSequence [A]

  abstract
    sequence : Seq [A]
    add : A -> SortedSequence [A]
    invariant : Boolean

end

class NonEmptySortedSequence [A subtype Comparable [A] ]
  extends
    SortedSequenceWithElements [A]

  abstract
    sequence : Seq [A]

  aux = NonEmptySortedSequenceAux_ [A] ()

  add_for (element : A) : SortedSequence [A] =
    NonEmptySortedSequence_ (aux .insert_sorted (sequence) (element) )

  add : A -> SortedSequence [A] =
    lambda element --> add_for (element)

  invariant : Boolean = aux .is_sorted (sequence)

end

class NonEmptySortedSequenceAux [A subtype Comparable [A] ]

  abstract

  is_less_than (x : A) (y : A) : Boolean =
    x .compareTo (y) < 0

  is_sorted (other_sequence : Seq [A] ) : Boolean =
    other_sequence
      .zip (other_sequence .tail)
      .forall (lambda pair --> is_less_than (pair ._1) (pair ._2) )

  concatenate (first_part : Seq [A] ) (middle : Seq [A] ) (last_part : Seq [A] ) : Seq [A] =
    first_part .++ (middle .++ (last_part) )

  insert_sorted (original_sequence : Seq [A] ) (element : A) : Seq [A] =
    concatenate (
      first_part := original_sequence.takeWhile (lambda x --> is_less_than (x) (element) ) ) (
      middle := Seq (element) ) (
      last_part := original_sequence.dropWhile (lambda x --> is_less_than (x) (element) )
    )

end

class SortedSequenceBuilder [A subtype Comparable [A] ]

  abstract

  import
    soda.lib.Fold_

  _fold = Fold .mk

  _initial_value : SortedSequence [A] = EmptySortedSequence_ [A] ()

  _next_value_function (sorted_sequence : SortedSequence [A] ) (element : A)
      : SortedSequence [A] =
    sorted_sequence .add (element)

  build (sequence : Seq [A] ) : SortedSequence [A] =
    _fold .apply [A] [SortedSequence [A] ] (sequence) (_initial_value) (_next_value_function)

end

