package soda.example.algorithms

class Parameter

  abstract
    name: String

end

class PatternMatching

  get_value (p: Parameter): Int =
    match p
      | Singleton_ (x) => x
      | Pair_ (x, y) => (x + y) / 2
      | Triplet_ (x, y, z) => (x + y + z) / 3
      | otherwise => 0
    end

  get_type_name (p: Parameter): String =
    match p
      | x: Singleton => x.name + "(x)"
      | x: Pair => x.name + "(x, y)"
      | x: Triplet => x.name + "(x, y, z)"
      | otherwise => ""
    end

end

class PatternMatching_ ()
  extends
    PatternMatching

end

class Singleton
  extends
    Parameter

  abstract
    x: Int

  name = "singleton"

end

class Singleton_ (x: Int)
  extends
    Singleton

end

class Pair
  extends
    Parameter

  abstract
    x: Int
    y: Int

  name = "pair"

end

class Pair_ (x: Int, y: Int)
  extends
    Pair

end

class Triplet
  extends
    Parameter

  abstract
    x: Int
    y: Int
    z: Int

  name = "triplet"

end

class Triplet_ (x: Int, y: Int, z: Int)
  extends
    Triplet

end

