package soda.example.inanutshell

class InANutshell

  f (x: Int): Int = x + 16

  b = f (a)

  a = 1 + 2 + 4 + 8

  h0 (x: Int) (y: Int): Int =
    let
      a = x + y
      b = x - y
    in a * b

  h1 (x: Int) (y: Int): Int =
    let
      a = x + y
      b = x - y
    in a * b

  h2 (x: Int) (y: Int): Int =
    let
      result = a * b
      a = x + y
      b = x - y
    in result

  is_greater_than (a: Int) (b: Int): Boolean =
    a > b

  max (a: Int) (b: Int): Int =
    if a > b
    then a
    else b

end

class MaxAndMin

  max (a: Int) (b: Int): Int =
    if a > b
    then a
    else b

  min (a: Int) (b: Int): Int =
    if a < b
    then a
    else b

end

class ConcreteMaxAndMin ()
  extends
    MaxAndMin

end

class MinMaxPair (min: Int, max: Int)

end

class Indexable

  abstract
    index: Int

end

class Example
  extends
    Indexable

  min_max (a: Int) (b: Int): MinMaxPair =
    MinMaxPair (
      min := ConcreteMaxAndMin ().min (a) (b),
      max := ConcreteMaxAndMin ().max (a) (b)
    )

end

class Example_ (index: Int)
  extends Example

class Comparable

  abstract
    is_greater_than (x: Comparable): Boolean

end

class ComparableMax [T subtype Comparable]

  max (a: T) (b: T): T =
    if a.is_greater_than (b)
    then a
    else b

end

class WithInstance

  abstract
    instance_parameter: Int

end

class MyClass
   extends
     WithInstance

  class_constant: Int = 1

  another_function (x: Int): Int = 2 * x

end

class TimeOfToday

  import
    java.util.Date

  get_time: Date = @new Date ()

end

class MainClass

  main (arguments: Array [String]): Unit =
    println ("Hello world!")

end

class Main ()
  extends
    MainClass

end

class PersonName

  abstract
    name: String

  @override
  toString = name

end

