
class ScoringScenarioExample

  abstract

  actors : Seq [Actor] =
    Seq (
      "Alice", "Benjamin", "Charlotte", "Daniel", "Emily", "Fiona", "George", "Hannah", "Isaac",
      "James", "Kevin", "Lily", "Matthew", "Natalie", "Olivia", "Quinn", "Peter", "Rachel",
      "Sarah", "Timothy", "Ursula", "Victoria", "William", "Xavier", "Yasmine", "Zachary"
    ) .map (lambda name --> Actor_ (name) )

  _resource_zero : Resource = Resource_ ("0")

  _resource_one : Resource = Resource_ ("1")

  _measure_zero : Measure = Measure_ (0)

  _measure_one : Measure = Measure_ (1)

  seed_protected_attribute : Long = 127

  protected_attribute_modulus : Int = 2

  mod (x : Int) (modulus : Int) : Int =
    ( (x % modulus) + modulus) % modulus

  as_protected_attribute (x : Int) : Int =
    mod (x) (protected_attribute_modulus)

  protected_attribute : Seq [Measure] =
    Random_ () .get_next_seq (seed_protected_attribute) (actors .length)
      .map (lambda x --> Measure_ ( as_protected_attribute (x .intValue) ) )

  protected_attribute_map : Map [Actor] [Measure] =
    actors
      .indices
      .map (lambda index -->
        Tuple2 [Actor] [Measure] (actors .apply (index) , protected_attribute .apply (index) ) )
      .toMap

  protected_attribute_function (a : Actor) : Measure =
    protected_attribute_map .getOrElse (a , _measure_zero )

  seed_result : Long = 65535

  prediction_modulus : Int = 100

  prediction_limit : Int = prediction_modulus / 2

  min (a : Int) (b : Int) : Int =
    if a < b
    then a
    else b

  as_prediction (x : Int) : Int =
    mod (min (x) (prediction_modulus - 1) ) (prediction_modulus)

  make_binary_resource (x : Int) : Resource =
    if x > prediction_limit
    then _resource_one
    else _resource_zero

  make_binary_measure (x : Int) : Measure =
    if x > prediction_limit
    then _measure_one
    else _measure_zero

  result_values : Seq [Int] =
    Random_ ()
      .get_next_seq (seed_result) (actors .length)
      .map (lambda x --> as_prediction ( (x .intValue) % prediction_modulus) )

  prediction_error : Int = 1

  prediction_bias_on_attribute : Int = 40

  maximum_acceptable_bias_percentage : Measure = Measure_ (30)

  result : Seq [Measure] =
    result_values .map (lambda x --> make_binary_measure (x) )

  result_map : Map [Actor] [Measure] =
    actors
      .indices
      .map (lambda index -->
        Tuple2 [Actor] [Measure] (actors .apply (index) , result .apply (index) ) )
      .toMap

  result_function (a : Actor) : Measure =
    result_map .getOrElse (a , _measure_zero )

  add_attribute_bias (index : Int) (original : Int) : Int =
    if (protected_attribute .apply (index) == _measure_zero)
    then original
    else as_prediction (original + prediction_bias_on_attribute)

  add_prediction_error (value : Int) : Int =
    as_prediction (value + prediction_error)

  unbiased_prediction : Seq [Resource] =
    result_values
      .map (lambda x --> add_prediction_error (x) )
      .map (lambda x --> make_binary_resource (x) )

  biased_prediction : Seq [Resource] =
    result_values
      .indices
      .map (lambda index --> add_attribute_bias (index) (result_values .apply (index) ) )
      .map (lambda x --> add_prediction_error (x) )
      .map (lambda x --> make_binary_resource (x) )

  unbiased_outcome : Outcome =
    Outcome_ (
      actors
        .indices
        .map (lambda index -->
          Assignment_ (actors .apply (index) ) (unbiased_prediction .apply (index) )
        )
    )

  biased_outcome : Outcome =
    Outcome_ (
      actors
        .indices
        .map (lambda index -->
          Assignment_ (actors .apply (index) ) (biased_prediction .apply (index) )
        )
    )

  evaluation (resource : Resource) : Measure =
    if (resource == _resource_zero)
    then _measure_zero
    else _measure_one

  context = Context_ ()

  initial_unbiased : TileMessage [Boolean] =
    TileMessageBuilder_ () .build (context) (unbiased_outcome) (true)

  initial_biased : TileMessage [Boolean] =
    TileMessageBuilder_ () .build (context) (biased_outcome) (true)

end

